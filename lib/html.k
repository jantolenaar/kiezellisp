;;;; Copyright (c) Jan Tolenaar. See the file LICENSE for details.

(require "xml")

(package "html-elements")

(def tags '(html head title etag meta link style body script
            div span form input button textarea center
            h1 h2 h3 h4 h5 h6
            a p i b code pre img samp big small
            ul ol li
            table thead tbody tr td th
            blockquote
            area base basefont col frame isindex param
            br hr))

(def empty-tags '(area base basefont br col frame hr img input isindex link meta param))

(def long-tags '(script textarea div))

(defun make-html-function (tag)
    (let name (symbol-name tag))
    (let func-name (string "<" name ">"))
    (let func (make-symbol func-name))
    (eval `(do
                (let html-specific-type 
                    (new 'xml::xml-base-type
                        :tag ,name
                        :supports-empty-tag ,(boolean (index-of tag empty-tags))
                        :has-long-tag ,(boolean (index-of tag long-tags))))
                (defun ,func (&rest args) 
                    (let target (new html-specific-type
                                    :attributes (new)
                                    :elements (vector)))
                    (xml:add-range target args))))
    (public func-name))

(each make-html-function tags)

(use-package-symbols "html-elements")

(package "html")

(public "DOC-TYPE"
        "to-string")

(def DOC-TYPE "<!DOCTYPE HTML>\n")

(defun to-string (obj &optional doc-type)
    (xml:to-string obj doc-type))

